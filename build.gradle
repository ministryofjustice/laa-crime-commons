plugins {
    id 'com.javiersc.semver.project' version "$semverVersion"
    id 'org.sonarqube' version "$sonarQubeVersion"
    id 'org.springframework.boot' version "$springframeworkBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id 'io.github.gradle-nexus.publish-plugin' version "$publishPluginVersion"
}

group = "uk.gov.justice.service.laa-crime"

subprojects {
    apply plugin: "java"

    java {
        withSourcesJar()
        withJavadocJar()
    }

    // Disable warnings for missing JavaDoc
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    group = "uk.gov.justice.service.laa-crime"

    repositories {
        mavenCentral()
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = project.findProperty("sonatypeUsername") ?: System.getenv("SONATYPE_USERNAME")
            password = project.findProperty("sonatypePassword") ?: System.getenv("SONATYPE_PASSWORD")
        }
    }
    useStaging.set(provider {
        !(project.property("isSnapshot").toString()).toBoolean()
    } as Provider<? extends Boolean>)
}

sonar {
    properties {
        property "sonar.projectName", "laa-crime-commons"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "ministryofjustice"
        property "sonar.projectKey", "ministryofjustice_laa-crime-commons"
    }
}

project(":crime-commons-spring-boot-starter-rest-client") {
    sonar {
        properties {
            property "sonar.coverage.jacoco.xmlReportPaths",
                    "${project(":crime-commons-spring-boot-starter-rest-client").buildDir}/reports/jacoco/test/jacocoTestReport.xml"
            property "sonar.exclusions", "**/RetryConfiguration.java, **/exception/**.java, **/ErrorDTO.java"
            property "sonar.coverage.exclusions", "**/RetryConfiguration.java, **/exception/**.java, **/ErrorDTO.java"
        }
    }
}


project(":crime-commons-classes") {
    sonar {
        properties {
            property "sonar.coverage.jacoco.xmlReportPaths",
                    "${project(":crime-commons-classes").buildDir}/reports/jacoco/test/jacocoTestReport.xml"
            property "sonar.exclusions", "**/exception/**.java, **/enums/**.java"
            property "sonar.coverage.exclusions", "**/exception/**.java, **/enums/**.java"
        }
    }
}

project(":crime-commons-samples") {
    apply plugin: "org.springframework.boot"
    sonar {
        skipProject = true
    }
}
