plugins {
    id 'com.javiersc.semver' version "$semverVersion"
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id 'org.springframework.boot' version "$springframeworkBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

semver {
    tagPrefix.set("crime-commons-classes-")
}

def versions = [
        jacksonDatabind  : "2.19.2",
        jacksonCore      : "2.19.2",
        jakartaValidation: "3.0.2",
        commonsioVersion : "2.16.1"
]

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {

    implementation platform("io.sentry:sentry-bom:7.0.0")
    implementation("io.sentry:sentry-spring-boot-starter-jakarta")
    implementation("io.sentry:sentry-logback")
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "io.swagger.core.v3:swagger-annotations:2.2.19"

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jacksonCore
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jacksonCore
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: versions.jakartaValidation
    implementation group: 'commons-io', name: 'commons-io', version: versions.commonsioVersion

    implementation "org.apache.commons:commons-lang3"
    implementation 'io.micrometer:micrometer-tracing'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    implementation "org.springframework.boot:spring-boot-starter-test"
}

publishing {
    publications {
        maven(MavenPublication) {

            from components.java

            pom {
                artifactId = project.name
                name = "Crime Apps Common Classes"
                url = "https://github.com/ministryofjustice/laa-crime-commons"
                description = "A Spring Boot auto-configuration library with all common classes"

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                organization {
                    name = "Justice Digital"
                }
                developers {
                    developer {
                        id = "Muthu"
                        name = "Muthu Subramanian"
                        email = "muthu.subramanian@digital.justice.gov.uk"
                    }
                }
                scm {
                    url = "https://github.com/ministryofjustice/laa-crime-commons.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

tasks.named("test") {
    useJUnitPlatform()
}


