plugins {
    id "jacoco"
    id "signing"
    id "maven-publish"
    id "io.spring.dependency-management"
    id "com.javiersc.semver.project" version "$semverVersion"
    id 'org.springframework.boot' version "$springframeworkBootVersion"
}

semver {
    tagPrefix.set("crime-commons-classes-")
}

def versions = [
        jacksonDatabind  : "2.18.1",
        jacksonCore      : "2.16.0",
        jakartaValidation: "3.0.2",
        commonsioVersion : "2.16.1"
]

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {


    implementation "org.apache.commons:commons-lang3"
    implementation "io.micrometer:micrometer-tracing"

    implementation "io.swagger.core.v3:swagger-annotations:2.2.19"

    implementation "org.springframework:spring-test"
    implementation "org.springframework:spring-webmvc"
    implementation "org.springframework.boot:spring-boot-starter-web"

    implementation "commons-io:commons-io:$versions.commonsioVersion"
    implementation "jakarta.validation:jakarta.validation-api:$versions.jakartaValidation"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

}

publishing {
    publications {
        maven(MavenPublication) {

            from components.java

            pom {
                artifactId = project.name
                name = "Crime Apps Common Classes"
                url = "https://github.com/ministryofjustice/laa-crime-commons"
                description = "A Spring Boot auto-configuration library with all common classes"

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                organization {
                    name = "Justice Digital"
                }
                developers {
                    developer {
                        id = "Muthu"
                        name = "Muthu Subramanian"
                        email = "muthu.subramanian@digital.justice.gov.uk"
                    }
                }
                scm {
                    url = "https://github.com/ministryofjustice/laa-crime-commons.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

tasks.named("test") {
    useJUnitPlatform()
}


